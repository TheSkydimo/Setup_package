; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

;OEMVer=1 仅用于 skydimo

; 下方三个版本宏由打包脚本自动维护：
;   MyAppVersion     : 安装器显示用的版本号（通常等于 ProductVersion，例如 2.0.2.r251201）
;   MyFileVersion    : 可执行文件的 FileVersion（例如 2.0.2.0）
;   MyProductVersion : 可执行文件的 ProductVersion（例如 2.0.2.r251201）
#define AppVersionFull "2.0.2.3223981"
#define AppVersionFile "2.0.2.0"

#define MyAppVersion AppVersionFull
#define MyProductVersion AppVersionFull
#define MyFileVersion AppVersionFile
#define MyAppName "AARGB"
#define MyAppPublisher "Shenzhen Guang yvzhou Technology Co., Ltd."
#define MyAppURL " "
#define MyAppExeName "AARGB.exe"

; Delete autorun on uninstall
;注册
[Registry]

;默认配置
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run";  ValueName: {#MyAppName}; ValueType: none; Flags: deletevalue;
;用户配置
Root: HKCU; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run";  ValueName: {#MyAppName}; ValueType: none; Flags: deletevalue;
;app表示系统额默认路径
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: {#MyAppName}; ValueData: """{app}\{#MyAppExeName}"" --auto_startup"; Flags:  uninsdeletevalue; Tasks:startup;   Check: IsAdminInstall;
Root: HKCU; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; ValueType: string; ValueName: {#MyAppName}; ValueData: """{app}\{#MyAppExeName}"" --auto_startup"; Flags:  uninsdeletevalue; Tasks:startup;   Check: not IsAdminInstall;

//设置
[Setup]
; PrivilegesRequired=admin
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{591829A4-48F0-4082-94B6-A5159B3EB49F}}
AppMutex=skydimo2_Main,Global\skydimo2_Main

AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
ArchitecturesInstallIn64BitMode=x64
ArchitecturesAllowed=x64
UninstallDisplayName={#MyAppName}
DisableProgramGroupPage=yes
ShowLanguageDialog=yes
; 根据系统 UI 语言自动选中匹配的安装语言；未匹配到则使用默认语言（英文）
LanguageDetectionMethod=uilanguage
;是否允许使用以前的语言
UsePreviousLanguage=no

LicenseFile=..\License\license_EN.txt
PrivilegesRequiredOverridesAllowed=dialog

//生成文件目录文件夹
OutputDir=..\Setup_package
//打开日志功能
SetupLogging=yes
OutputBaseFilename=AARGBSetup_{#AppVersionFull}

;SetupIconFile=D:\Users\Super888\Documents\DaLed\Setup\main.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern   

;覆盖安装
UsePreviousAppDir = no

//LanguageDetectionMethod=uilanguage
//DefaultDialogFontName=Tahoma

;ShowLanguageDialog=auto

//语言
[Languages]
Name: "chinesesimplified"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"; LicenseFile: "..\License\license_CN.txt"
Name: "english"; MessagesFile: "compiler:Default.isl";
Name: "arabic"; MessagesFile: "compiler:Languages\Arabic.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"
Name: "korean"; MessagesFile: "compiler:Languages\Korean.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "Vietnamese"; MessagesFile: "compiler:Languages\Vietnamese.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "turkish"; MessagesFile: "compiler:Languages\Turkish.isl"

//定义安装中可选任务
[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; 
Name: "startup"; Description: "{cm:AutoStartProgram,{#MyAppName}}"; GroupDescription: "{cm:AdditionalIcons}"; 
;Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: checkablealone


[Files]
#define Source_path "..\AARGB"
; 临时用于静默安装（装完删除）
Source: "{#Source_path}\VC\vc_redist.x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
; 可选：保留一份离线包给用户手动安装
Source: "{#Source_path}\VC\vc_redist.x64.exe"; DestDir: "{app}\Redist\VC"

Source: "{#Source_path}\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion signonce; 
Source: "{#Source_path}\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs; Excludes: "*.exe"
Source: "{#Source_path}\VC\vc_redist.x64.exe"; DestDir: "{app}\VC"; Flags: ignoreversion


[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{userdesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; 


[Code]
const
  VC_KEY    = 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64';
  // 最低要求，可按需提升（例如 14.40+）
  MIN_MAJOR = 14;
  MIN_MINOR = 38;
  MIN_BLD   = 0;
  MIN_RBLD  = 0;

function IsVCRedistMissing(): Boolean;
var
  Installed, Major, Minor, Bld, RBld: Cardinal;
begin
  Result := True; // 默认：需要安装

  if not RegQueryDWordValue(HKLM, VC_KEY, 'Installed', Installed) then Exit;
  if Installed <> 1 then Exit;

  if RegQueryDWordValue(HKLM, VC_KEY, 'Major', Major) and
     RegQueryDWordValue(HKLM, VC_KEY, 'Minor', Minor) and
     RegQueryDWordValue(HKLM, VC_KEY, 'Bld',   Bld) and
     RegQueryDWordValue(HKLM, VC_KEY, 'RBld',  RBld) then
  begin
    // 达到或高于最低版本 → 不需要安装
    if (Major > MIN_MAJOR) or
       ((Major = MIN_MAJOR) and (Minor > MIN_MINOR)) or
       ((Major = MIN_MAJOR) and (Minor = MIN_MINOR) and (Bld > MIN_BLD)) or
       ((Major = MIN_MAJOR) and (Minor = MIN_MINOR) and (Bld = MIN_BLD) and (RBld >= MIN_RBLD)) then
      Result := False;
  end
  else
  begin
    // 没有细分版本但 Installed=1 → 视为已满足，避免误装
    Result := False;
  end;
end;
 

procedure CurStepChanged(CurStep: TSetupStep);
var
  logfilepathname, logfilename, newfilepathname: string;
begin
  logfilepathname := ExpandConstant('{log}');
  logfilename := ExtractFileName(logfilepathname);
  newfilepathname := ExpandConstant('{app}\') + logfilename;
  
  if CurStep = ssDone then
  begin
    FileCopy(logfilepathname, newfilepathname, false);
  end;
end; 


function IsAdminInstall(): Boolean;
begin
  // 检查安装是否为所有用户，这通常意味着需要管理员权限
  Result := (IsAdmin and (GetShellFolderByCSIDL($30, True) = ExpandConstant('{commonappdata}')));
end;


[InstallDelete]
Type: filesandordirs; Name: "{app}"

[UninstallDelete]
Type: filesandordirs; Name: "{app}"

#IFDEF UNICODE
  #DEFINE AW "W"
#ELSE
  #DEFINE AW "A"
#ENDIF
[Run]
Filename: "{tmp}\vc_redist.x64.exe"; \
    Parameters: "/install /quiet /norestart"; \
    Flags: runhidden waituntilterminated; \
    StatusMsg: "Installing Microsoft VC++ Runtime..."; \
    Check: IsVCRedistMissing()
Filename: "{app}\{#MyAppExeName}"; \
    Description: "{cm:LaunchProgram,{#MyAppName}}"; \
    WorkingDir: "{app}"; \
    Flags: nowait postinstall skipifsilent runasoriginaluser