; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

; OEMVer=1 仅用于 Skydimo

; 下方三个版本宏由打包脚本自动维护：
;   MyAppVersion     : 安装器显示用的短版本号（例如 2.0.2）
;   MyFileVersion    : 可执行文件的 FileVersion（例如 2.0.2.0）
;   MyProductVersion : 可执行文件的 ProductVersion（例如 2.0.2.6e4c602）
#define MyAppVersion "2.0.2.6e4c602"
#define MyFileVersion "2.0.2.0"
#define MyProductVersion "2.0.2.6e4c602"
#define MyAppName "Skydimo"
#define MyAppPublisher "Shenzhen Guang yvzhou Technology Co., Ltd."
#define MyAppURL "https://www.skydimo.com"
#define MyAppExeName "Skydimo.exe"

;------------------------------------------------------
; 注册表：开机自启动相关
;------------------------------------------------------
[Registry]
; 默认配置：卸载时删除开机启动项
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run";  ValueName: {#MyAppName}; ValueType: none; Flags: deletevalue;
; 用户配置：卸载时删除开机启动项
Root: HKCU; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run";  ValueName: {#MyAppName}; ValueType: none; Flags: deletevalue;

; app 表示系统默认安装路径
; 管理员安装 → HKLM
Root: HKLM; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; \
    ValueType: string; ValueName: {#MyAppName}; \
    ValueData: """{app}\{#MyAppExeName}"" --auto_startup"; \
    Flags: uninsdeletevalue; Tasks: startup; Check: IsAdminInstall;

; 非管理员安装 → HKCU
Root: HKCU; Subkey: "SOFTWARE\Microsoft\Windows\CurrentVersion\Run"; \
    ValueType: string; ValueName: {#MyAppName}; \
    ValueData: """{app}\{#MyAppExeName}"" --auto_startup"; \
    Flags: uninsdeletevalue; Tasks: startup; Check: not IsAdminInstall;

;------------------------------------------------------
; 安装基础设置
;------------------------------------------------------
[Setup]
; PrivilegesRequired=admin
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{591829A4-48F0-4082-94B6-A5159B3EB49F}}
AppMutex=skydimo2_Main,Global\skydimo2_Main

AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
ArchitecturesInstallIn64BitMode=x64
ArchitecturesAllowed=x64
UninstallDisplayName={#MyAppName}
DisableProgramGroupPage=yes
ShowLanguageDialog=yes

; 许可证文件放在项目根目录的 License 目录下
; 本脚本位于 iss 目录，因此使用 ..\License\ 相对路径
LicenseFile=..\License\license.txt
;InfoBeforeFile=D:\Users\Super888\Documents\DaLed\Setup\License\license.txt

; 可切换为当前用户安装
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog

; 生成输出目录（相对于项目根目录，而不是 iss 子目录）
; 当前脚本位于 {项目根}\iss，下方使用 ..\Setup_package 指向根目录下的 Setup_package
OutputDir=..\Setup_package
; 打开日志功能
SetupLogging=yes
OutputBaseFilename=SkydimoSetup

;SetupIconFile=D:\Users\Super888\Documents\DaLed\Setup\main.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern

; 覆盖安装时不使用之前的安装目录
UsePreviousAppDir=no

;ShowLanguageDialog=auto

;------------------------------------------------------
; 多语言
;------------------------------------------------------
[Languages]
; 中文简体使用单独的中文协议文件，同样位于上级 License 目录
Name: "chinesesimplified"; MessagesFile: "compiler:Languages\ChineseSimplified.isl"; LicenseFile: "..\License\license_cn.txt"
Name: "english"; MessagesFile: "compiler:Default.isl";
Name: "arabic"; MessagesFile: "compiler:Languages\Arabic.isl"
Name: "german"; MessagesFile: "compiler:Languages\German.isl"
Name: "spanish"; MessagesFile: "compiler:Languages\Spanish.isl"
Name: "japanese"; MessagesFile: "compiler:Languages\Japanese.isl"
Name: "korean"; MessagesFile: "compiler:Languages\Korean.isl"
Name: "portuguese"; MessagesFile: "compiler:Languages\Portuguese.isl"
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"
Name: "Vietnamese"; MessagesFile: "compiler:Languages\Vietnamese.isl"
Name: "french"; MessagesFile: "compiler:Languages\French.isl"
Name: "turkish"; MessagesFile: "compiler:Languages\Turkish.isl"

;------------------------------------------------------
; 安装任务
;------------------------------------------------------
[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}";
Name: "startup"; Description: "{cm:AutoStartProgram,{#MyAppName}}"; GroupDescription: "{cm:AdditionalIcons}";
;Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: checkablealone

;------------------------------------------------------
; 文件复制
;------------------------------------------------------
[Files]
#define Source_path "..\Skydimo"

; VC++ 2015–2022 x64 运行库
; 1) 临时：用于静默安装（装完删除）
Source: "{#Source_path}\VC\vc_redist.x64.exe"; DestDir: "{tmp}"; Flags: deleteafterinstall
; 2) 永久：保留离线安装包
Source: "{#Source_path}\VC\vc_redist.x64.exe"; DestDir: "{app}\VC"; Flags: ignoreversion

; CH340/CH341 驱动安装程序
; 1) 临时：用于静默安装（装完删除）
Source: "{#Source_path}\CH340\CH341SER.EXE"; DestDir: "{tmp}"; Flags: deleteafterinstall
; 2) 永久：保留在自己的目录，方便以后手动安装 / 排查
Source: "{#Source_path}\CH340\CH341SER.EXE"; DestDir: "{app}\CH340"; Flags: ignoreversion

; 主程序
Source: "{#Source_path}\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion signonce;

; 其它数据文件（不包含 exe）
Source: "{#Source_path}\*"; DestDir: "{app}"; \
    Flags: ignoreversion recursesubdirs createallsubdirs; \
    Excludes: "*.exe"

;------------------------------------------------------
; 快捷方式
;------------------------------------------------------
[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{userdesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon;

;------------------------------------------------------
; 代码区
;------------------------------------------------------
[Code]
const
  VC_KEY    = 'SOFTWARE\Microsoft\VisualStudio\14.0\VC\Runtimes\x64';
  MIN_MAJOR = 14;
  MIN_MINOR = 38;
  MIN_BLD   = 0;
  MIN_RBLD  = 0;

function IsVCRedistMissing(): Boolean;
var
  Installed, Major, Minor, Bld, RBld: Cardinal;
begin
  Result := True; 

  if not RegQueryDWordValue(HKLM, VC_KEY, 'Installed', Installed) then
    Exit;
  if Installed <> 1 then
    Exit;

  if RegQueryDWordValue(HKLM, VC_KEY, 'Major', Major) and
     RegQueryDWordValue(HKLM, VC_KEY, 'Minor', Minor) and
     RegQueryDWordValue(HKLM, VC_KEY, 'Bld',   Bld) and
     RegQueryDWordValue(HKLM, VC_KEY, 'RBld',  RBld) then
  begin
   
    if (Major > MIN_MAJOR) or
       ((Major = MIN_MAJOR) and (Minor > MIN_MINOR)) or
       ((Major = MIN_MAJOR) and (Minor = MIN_MINOR) and (Bld > MIN_BLD)) or
       ((Major = MIN_MAJOR) and (Minor = MIN_MINOR) and (Bld = MIN_BLD) and (RBld >= MIN_RBLD)) then
      Result := False;
  end
  else
  begin
    
    Result := False;
  end;
end;


function IsCH340Missing(): Boolean;
begin
  Result := True;  // 默认需要安装

  // 情况 1：官方 CH341 驱动存在
  if FileExists(ExpandConstant('{sys}\drivers\CH341S64.SYS')) then
  begin
    Result := False;
    exit;
  end;

  // 情况 2：微软内置 USB-Serial 驱动存在（Win10/11 默认）
  if FileExists(ExpandConstant('{sys}\drivers\usbser.sys')) then
  begin
    Result := False;
    exit;
  end;

  // 情况 3：服务存在（设备插入时通常会出现）
  if RegKeyExists(HKLM, 'SYSTEM\CurrentControlSet\Services\CH341SER') then
  begin
    Result := False;
    exit;
  end;

  if RegKeyExists(HKLM, 'SYSTEM\CurrentControlSet\Services\wchusbser') then
  begin
    Result := False;
    exit;
  end;

  if RegKeyExists(HKLM, 'SYSTEM\CurrentControlSet\Services\usbser') then
  begin
    Result := False;
    exit;
  end;
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
  logfilepathname, logfilename, newfilepathname: string;
begin
  logfilepathname := ExpandConstant('{log}');
  logfilename := ExtractFileName(logfilepathname);
  newfilepathname := ExpandConstant('{app}\') + logfilename;

  if CurStep = ssDone then
  begin
    FileCopy(logfilepathname, newfilepathname, False);
  end;
end;


function IsAdminInstall(): Boolean;
begin
 
  Result := (IsAdmin and (GetShellFolderByCSIDL($30, True) = ExpandConstant('{commonappdata}')));
end;


[InstallDelete]
Type: filesandordirs; Name: "{app}"

[UninstallDelete]
Type: filesandordirs; Name: "{app}"

#IFDEF UNICODE
  #DEFINE AW "W"
#ELSE
  #DEFINE AW "A"
#ENDIF

;------------------------------------------------------
; 安装后执行的程序
;------------------------------------------------------
[Run]
; 按需安装 VC++ 运行库
Filename: "{tmp}\vc_redist.x64.exe"; \
    Parameters: "/install /quiet /norestart"; \
    Flags: runhidden waituntilterminated; \
    StatusMsg: "Installing Microsoft VC++ Runtime..."; \
    Check: IsVCRedistMissing()

; 按需安装 CH340/CH341 驱动（仅在缺失且为管理员时执行）
Filename: "{tmp}\CH341SER.EXE"; \
    Parameters: "/SILENT /NORESTART"; \
    Flags: runhidden waituntilterminated; \
    StatusMsg: "Installing CH340/CH341 USB-Serial driver..."; \
    Check: IsCH340Missing() and IsAdmin

; 安装完成后可选启动主程序
Filename: "{app}\{#MyAppExeName}"; \
    Description: "{cm:LaunchProgram,{#MyAppName}}"; \
    WorkingDir: "{app}"; \
    Flags: nowait postinstall skipifsilent runasoriginaluser
